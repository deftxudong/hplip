#
#  configure.in - hpijs autoconf input file
#
#  (c) 2001-2004 Copyright Hewlett-Packard Development Company, LP
#
#  Redistribution and use in source and binary forms, with or without
#  modification, are permitted provided that the following conditions
#  are met:
#  1. Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer.
#  2. Redistributions in binary form must reproduce the above copyright
#     notice, this list of conditions and the following disclaimer in the
#     documentation and/or other materials provided with the distribution.
#  3. Neither the name of the Hewlett-Packard nor the names of its
#     contributors may be used to endorse or promote products derived
#     from this software without specific prior written permission.
#
#  THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
#  IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
#  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
#  IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
#  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
#  NOT LIMITED TO, PATENT INFRINGEMENT; PROCUREMENT OF SUBSTITUTE GOODS OR
#  SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
#  HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
#  STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
#  IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#  POSSIBILITY OF SUCH DAMAGE.

AC_PREREQ([2.59])
AC_INIT([HP IJS printer driver],
	[2.1.10],
	[hpijs@packages.debian.org],
	[hpijs])

AC_COPYRIGHT(
[Copyright (c) 2001-2004 Hewlett-Packard Development Company, LP
This configure script is free software; you can redistribute it and/or modify
it provided that the following conditions are met:

  1. Redistributions of source code must retain the above copyright
     notice, this list of conditions and the following disclaimer.
  2. Redistributions in binary form must reproduce the above copyright
     notice, this list of conditions and the following disclaimer in the
     documentation and/or other materials provided with the distribution.
  3. Neither the name of the Hewlett-Packard nor the names of its
     contributors may be used to endorse or promote products derived
     from this software without specific prior written permission.

  THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
  IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
  IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
  NOT LIMITED TO, PATENT INFRINGEMENT; PROCUREMENT OF SUBSTITUTE GOODS OR
  SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
  HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
  STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
  IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
  POSSIBILITY OF SUCH DAMAGE.

])

AC_CONFIG_SRCDIR([hpijs.cpp])
AC_CONFIG_AUX_DIR([../..])
AM_INIT_AUTOMAKE([1.9 foreign])
AM_MAINTAINER_MODE
AC_PROG_LIBTOOL

# Check for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_INSTALL

dnl Checks for libraries
AC_CHECK_LIB([jpeg], [jpeg_set_defaults],,
	[AC_MSG_ERROR(["cannot find libjpeg support"])]
)

# Check for header files.
AC_HEADER_STDC

AC_CHECK_HEADERS([fcntl.h malloc.h syslog.h machine/types.h])
AC_CHECK_HEADERS(jpeglib.h,,
	[AC_MSG_ERROR([cannot find libjpeg-devel support])]
)

# Check for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_C_BIGENDIAN(,[MYFLAGS="-DAPDK_LITTLE_ENDIAN"])

MYFLAGS="$MYFLAGS -DAPDK_DJ660 -DAPDK_DJ6xx -DAPDK_DJ6xxPhoto \
-DAPDK_DJ8xx -DAPDK_DJ9xx -DAPDK_DJ9xxVIP -DAPDK_DJ630 \
-DAPDK_APOLLO2XXX -DAPDK_APOLLO21XX -DAPDK_APOLLO2560 -DAPDK_DJ600 \
-DAPDK_DJ350 -DAPDK_DJ8x5 -DAPDK_PSP100 -DAPDK_AUTODUPLEX \
-DAPDK_HIGH_RES_MODES -DAPDK_LJMONO -DAPDK_DJ540 -DAPDK_DJ850 \
-DAPDK_DJ890 -DAPDK_DJ3320 -DAPDK_LJCOLOR -DAPDK_DJGENERICVIP \
-DAPDK_LJJETREADY  -DAPDK_LJFASTRASTER \
-DAPDK_BUFFER_SEND -DAPDK_LDL_COMPRESS -DAPDK_EXTENDED_MEDIASIZE \
-DAPDK_MLC_PRINTER -DAPDK_DJ3600 -DAPDK_LINUX -DNDEBUG"

dnl autoconf-style header tests for APDK
cat >auto-include.h <<EOFH
#ifdef HAVE_INTTYPES_H
#include <inttypes.h>
#endif
#ifdef HAVE_STDINT_H
#include <stdint.h>
#endif
#ifdef HAVE_MACHINE_TYPES_H
#include <machine/types.h>
#endif
EOFH
AC_CHECK_TYPES([uint32_t],
	[MYFLAGS="$MYFLAGS -DAPDK_AUTO_INCLUDE"],,
	[#include "auto-include.h"]
)

CXXFLAGS="$CXXFLAGS $MYFLAGS"
CFLAGS="$CFLAGS $MYFLAGS"

AC_MSG_CHECKING([for platform-dependencies])
case "$host" in
   *-darwin*)
      AC_MSG_RESULT([using Mac OS X platform.h])
      cat >platform.h <<EOF
#include <stdlib.h>
#include <sys/types.h>
#include <sys/malloc.h>
#include <memory.h>
#include <string.h>
#include <stdio.h>
#include <math.h>
EOF
      ;;

     *)
      AC_MSG_RESULT([using Default platform.h])
      cat >platform.h <<EOF
#include <stdlib.h>
#include <memory.h>
#include <string.h>
#include <stdio.h>
#include <math.h>
EOF
      ;;
esac


CUPSSEARCHPATH="/usr/share /usr/local/share /usr/lib /usr/local/lib /usr/libexec /usr/lib64 /opt"

AC_ARG_VAR([CUPS_PPD_PATH], [CUPS ppd directory])
AC_MSG_CHECKING([for cups ppd path])
AS_IF([test "x${CUPS_PPD_PATH}" = "x"],
	[
	CUPS_PPD_PATH="/usr/share/cups/model"
	for i in $CUPSSEARCHPATH; do
	   if test -d "$i/cups/model"; then
	      CUPS_PPD_PATH="$i/cups/model"
	      break
	   fi
	done
	])
AC_MSG_RESULT([using ${CUPS_PPD_PATH}])

AC_ARG_VAR([CUPS_FILTER_PATH], [CUPS filters directory])
AC_MSG_CHECKING([for cups filter path])
AS_IF([test "x${CUPS_FILTER_PATH}" = "x"],
	[
	CUPS_FILTER_PATH="/usr/lib/cups/filter"
	for i in $CUPSSEARCHPATH; do
	   if test -d "$i/cups/filter"; then
	      CUPS_FILTER_PATH="$i/cups/filter"
	      break
	   fi
	done
	])
AC_MSG_RESULT([using ${CUPS_FILTER_PATH}])

AC_MSG_CHECKING([for cups install])
AC_ARG_ENABLE([cups_install],
	AS_HELP_STRING([--enable-cups-install],
	  	[enable cups install [default=yes]]),
	[cups_install=$enableval],
	[cups_install=yes])
if test $cups_install = yes; then
   AC_MSG_RESULT([yes])
else
   AC_MSG_RESULT([no])
fi
AM_CONDITIONAL([CUPS_INSTALL], [test x$cups_install = xyes])

AC_MSG_CHECKING([for foomatic install])
AC_ARG_ENABLE([foomatic_install],
	AS_HELP_STRING([--enable-foomatic-install],
	  	[enable foomatic install (default=yes)]),
	[foomatic_install=$enableval],
	[foomatic_install=yes])
AS_IF([test x$foomatic_install = xyes],
   [AC_MSG_RESULT([yes])],
   [AC_MSG_RESULT([no])]
)
AM_CONDITIONAL([FOOMATIC_INSTALL], [test x$foomatic_install = xyes])

AC_MSG_CHECKING([for HPLIP-enabled build])
AC_ARG_ENABLE([hplip-build],
	AS_HELP_STRING([--enable-hplip-build],
		[enable build for hplip (default=no)]),
	[hplip_build="$enableval"],
	[hplip_build=no])
AS_IF([test x$hplip_build = xyes],
	[AC_DEFINE([HAVE_LIBHPIP], [1], [Define if libhpip from HPLIP is available])
	 AC_MSG_RESULT([yes])],
	[AC_MSG_RESULT([no])]
)
AM_CONDITIONAL([HPLIP_BUILD], [test x$hplip_build = xyes])

dnl Checks for library functions.
AC_CHECK_FUNCS([strtod strtol],,
	[AC_MSG_ERROR([Cannot find required function])]
)
AC_CHECK_FUNC([pow],,
	[AC_CHECK_LIB([m], [pow],,
		[AC_MSG_ERROR([Cannot find required pow function])]
	)]
)

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
